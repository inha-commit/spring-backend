plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.ec4j.editorconfig' version '0.0.3'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.11.0'
}

// Spotless 설정
spotless {
    java {
        target("**/*.java")
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithTabs()
        endWithNewline()
        googleJavaFormat().aosp()
        importOrder 'java', 'javax', 'org', 'com', 'com.diffplug', ''
    }
}
//GitHooks를 이용한 pre-commit 단계에서 spotlessApply자동 적용
tasks.register('updateGitHooks', Copy) {
    from './scripts/pre-commit'
    into './.git/hooks'
    fileMode 0775
}
compileJava.dependsOn updateGitHooks

// EditorConfig 설정
editorconfig {
    excludes = ['build'] // build 디렉토리는 검사에서 제외함.
}

// Checkstyle 작업 설정
tasks.withType(Checkstyle) {
    reports {
        xml.required = true // xml 형식의 보고서가 필수적으로 작성되어야 함.
        html.required = true // html 형식의 보고서가 필수적으로 작성되어야 함.
    }
}
checkstyle {
    maxWarnings = 0
    configFile = file("${rootDir}/config/checkstyle/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "${rootDir}/config/checkstyle/naver-checkstyle-suppressions.xml"]
    toolVersion = "8.42"
}

// 프로젝트 정보 및 Java 설정
group = 'com.example'
version = '0.0.1-SNAPSHOT'
java {
    sourceCompatibility = '17'
}

// 의존성 설정
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    runtimeOnly("com.mysql:mysql-connector-j")
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'mysql:mysql-connector-java:8.0.32'

    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// 테스트 작업 설정
tasks.named('test') {
    useJUnitPlatform()
}
